/*
 * Copyright (c) 2020 Vikash Madhow
 */

/**
 * ESQL.
 *
 * @author Vikash Madhow (vikash.madhow@gmail.com)
 */

buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
}

plugins {
  id "antlr"
  id "java-library"
  id "maven-publish"
  id "signing"
  id "io.codearte.nexus-staging" version "0.22.0"
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

generateGrammarSource {
  outputDirectory = file("src/main/antlr")
  arguments += ["-long-messages"]
}

sourceSets {
  main {
    java { srcDirs = ['src/main/java', 'src/main/antlr'] }
    resources { srcDirs = ['src/main/resources'] }
  }
  test {
    java { srcDirs = ['src/test/java', 'src/test/antlr'] }
    resources { srcDirs = ['src/test/resources'] }
  }
}

dependencies {
  antlr "org.antlr:antlr4:latest.release"

  compile "org.apache.commons:commons-lang3:3.9"

  compile "org.postgresql:postgresql:42.2.18.jre7"
  compile "com.microsoft.sqlserver:mssql-jdbc:8.4.0.jre14"
  compile "org.hsqldb:hsqldb:2.5.1"
  compile "com.zaxxer:HikariCP:latest.release"

  compile "org.json:json:latest.release"
  compile "org.yaml:snakeyaml:latest.release"

  compile "com.vikmad:base:latest.release"

  // Use JUnit test framework
  testImplementation(platform('org.junit:junit-bom:5.7.0'))
  testImplementation('org.junit.jupiter:junit-jupiter:5.7.0')
}

test {
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
}

group = 'com.vikmad'
archivesBaseName = 'esql'
sourceCompatibility = JavaVersion.VERSION_14
version = "0.1.2"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  repositories {
    maven {
      def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
      url = isReleaseVersion ? releaseRepo : snapshotRepo
      credentials {
        username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
        password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        groupId = 'com.vikmad'
        artifactId = 'esql'
        name = "ESQL language implementation"
        description = 'ESQL, SQL enhanced with metadata compiling to various relational databases'
        url = 'https://github.com/vikashmadhow/esql'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = "vikashmadhow"
            name = "Vikash Madhow"
            email = "vikash.madhow@gmail.com"
            url = "http://vikmad.com"
          }
        }
        scm {
          url = 'https://github.com/vikashmadhow/esql'
          connection = 'scm:https://github.com/vikashmadhow/esql.git'
          developerConnection = 'scm:git://github.com/vikashmadhow/esql.git'
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
  onlyIf { isReleaseVersion }
}

nexusStaging {
  packageGroup = group
  username = project.getProperty('ossrhUsername')
  password = project.getProperty('ossrhPassword')
}
